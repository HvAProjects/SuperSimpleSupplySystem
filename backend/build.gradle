plugins {
	id 'org.springframework.boot' version '2.4.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.google.cloud.tools.jib' version '3.0.0'
	id 'de.fuerstenau.buildconfig' version '1.1.8'
	id 'idea'
	id "org.sonarqube" version "3.0"
	id "org.flywaydb.flyway" version "8.0.0-beta2"
}

group = 'nl.jed'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	maven { url 'https://repo.spring.io/milestone' }
	mavenCentral()
}

ext {
	set('testcontainersVersion', "1.15.1")
	set('springdocVersion', "1.5.10")
	ext.getGitHash = { ->
		def stdout = new ByteArrayOutputStream()
		exec {
			commandLine 'git', 'rev-parse', '--short', 'HEAD'
			standardOutput = stdout
		}
		return stdout.toString().trim()
	}
	ext.getTimestamp = { ->
		return new Date().format('yyyyMMddHHmmssSSS')
	}
}

buildConfig {
	buildConfigField "String", "GitHash", "${getGitHash()}"
}

sourceSets.main.java.srcDirs += "build/gen/buildconfig/src/main"

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-integration'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-configuration-processor'
	implementation 'org.flywaydb:flyway-core'
	implementation 'com.squareup.retrofit2:retrofit:2.4.0'
	implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
	implementation 'org.hibernate.reactive:hibernate-reactive-core:1.0.0.CR9'
	implementation 'org.hibernate:hibernate-jpamodelgen'
	implementation 'org.springframework.data:spring-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.4'
	runtimeOnly 'io.vertx:vertx-mysql-client:4.1.4'

	compileOnly 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'

	testCompile 'org.junit.jupiter:junit-jupiter-params:5.7.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.integration:spring-integration-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mariadb'
	testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'

//	swagger
	implementation group: 'org.springdoc', name: 'springdoc-openapi-webflux-ui', version: "${springdocVersion}"
	implementation group: 'org.springdoc', name: 'springdoc-openapi-webflux-core', version: "${springdocVersion}"
	implementation group: 'org.springdoc', name: 'springdoc-openapi-security', version: "${springdocVersion}"


	testCompile("io.github.bonigarcia:webdrivermanager:3.8.1")
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

jib {
	to {
		image = 'ghcr.io/hvaprojects/ssss_backend'
		auth {
			username = 'davidsoff'
			password = file('key').text
		}
		tags = ["main-${getGitHash()}-${getTimestamp()}"]
	}
	container {
		ports = ['8080']
		labels = ['org.opencontainers.image.source':'https://github.com/HvAProjects/SuperSimpleSupplySystem']
		format = 'OCI'
	}
}
