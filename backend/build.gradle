plugins {
	id 'org.springframework.boot' version '2.4.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.google.cloud.tools.jib' version '2.7.1'
	id 'de.fuerstenau.buildconfig' version '1.1.8'
	id 'idea'
}

group = 'nl.jed'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('testcontainersVersion', "1.15.1")
	ext.getGitHash = { ->
		def stdout = new ByteArrayOutputStream()
		exec {
			commandLine 'git', 'rev-parse', '--short', 'HEAD'
			standardOutput = stdout
		}
		return stdout.toString().trim()
	}
}

buildConfig {
	buildConfigField "String", "GitHash", "${getGitHash()}"
}

sourceSets.main.java.srcDirs += "build/gen/buildconfig/src/main"

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-integration'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-configuration-processor'
	implementation 'org.springframework.security:spring-security-oauth2-client'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springframework.integration:spring-integration-jpa'
	implementation 'org.springframework.integration:spring-integration-mail'
	implementation 'org.springframework.integration:spring-integration-security'
	implementation 'org.springframework.integration:spring-integration-webflux'
//	implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
//	implementation group: 'org.springframework', name: 'spring-webmvc', version: '5.3.3'
//	implementation group: 'org.springframework', name: 'spring-web', version: '5.3.3'
//	implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client', version: '5.4.4'
	implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
//	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.4.2'
	compileOnly 'org.projectlombok:lombok'
	compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.integration:spring-integration-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mariadb'
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 1
			}
		}
	}
}

check.dependsOn(jacocoTestCoverageVerification)

tasks.register('ci'){
	dependsOn tasks.build
	dependsOn tasks.jib
}

jib {
	to {
		image = 'ghcr.io/hvaprojects/ssss_backend'
		auth {
			username = 'davidsoff'
			password = file('key').text
		}
		tags = ["${getGitHash()}"]
	}
	container {
		ports = ['8081']
		labels = ['org.opencontainers.image.source':'https://github.com/HvAProjects/SuperSimpleSupplySystem']
		format = 'OCI'
	}
}
